// RISC-ICE-V
// inspired by https://github.com/sylefeb/Silice/blob/master/projects/ice-v/ice-v.ice
//
// A simple Risc-V RV32IMAFC processor

// RISC-V - MAIN CPU LOOP
//          ALU FUNCTIONALITY LISTED IN ALU-

// ON CPU INSTRUCTION CACHE DEFINITIONS
$$ addr_width = 27

// L0 CACHE SIZES FOR HART ID 0 AND 1
// MAX size is 64 due to bram limits
// size and blocks must be a power of 2
$$ L0Isize = 64
$$ L0Icacheaddrwidth = clog2(L0Isize)

// HART 0 - MAIN
$$ L00Iblocks = 8
$$ L00Icount = clog2(L00Iblocks)
$$ L00Ipartaddresswidth = addr_width - 1 - L00Icount - L0Icacheaddrwidth
$$ L00Ipartaddressstart = 1 + L00Icount + L0Icacheaddrwidth
bitfield L00cacheI{ uint30 instruction, uint1 compressed }

// HART 1 - SMT
$$ L01Iblocks = 4
$$ L01Icount = clog2(L01Iblocks)
$$ L01Ipartaddresswidth = addr_width - 1 - L01Icount - L0Icacheaddrwidth
$$ L01Ipartaddressstart = 1 + L01Icount + L0Icacheaddrwidth
bitfield L01cacheI{ uint30 instruction, uint1 compressed }

unit PAWSCPU(
    input   uint1   clock_CPUdecoder,
    input   uint1   clock_cache,
    output  uint2   accesssize,
    output  uint27  address(0),
    output  uint1   cacheselect(0),
    output  uint16  writedata,
    output  uint1   writememory,
    input   uint16  readdata,
    output  uint1   readmemory,
    input   uint1   memorybusy,

    // SMT
    input   uint1   SMTRUNNING,
    input   uint27  SMTSTARTPC,

    // MINI DMA CONTROLLER
    input   uint27  DMASOURCE,
    input   uint27  DMADEST,
    input   uint27  DMACOUNT,
    input   uint3   DMAMODE,
    output! uint2   DMAACTIVE(0)
) <reginputs> {
    // CPU REGISTERS AND THREAD SELECTION
    uint1   COMMIT = uninitialized;                 uint1   SMT = 0;                                    uint1   COMMIT_SMT = uninitialised;
    uint32  instruction = uninitialized;            uint1   compressed = uninitialized;
    uint27  pc = 0;                                 uint27  pc_next <:: SMT ? pc :  AGU.newPC;                                      // HART 0 pc + UPDATE
    uint27  pcSMT = uninitialized;                  uint27  pcSMT_next <:: SMT ? AGU.newPC : SMTRUNNING ? pcSMT : SMTSTARTPC;       // HART 1 pc + update
    uint27  PC <:: SMT ? pcSMT : pc;                                                                                                // SELECT PC FOR THIS CYCLE

    // RISC-V 32 BIT CONTROLLER
    uint32  result <: RV32CONTROL.FASTPATH ? EXECUTEFAST.result : EXECUTESLOW.result;
    uint1   frd <:: RV32CONTROL.FASTPATH ? RV32CONTROL.writeRegisterF : EXECUTESLOW.frd;
    uint1   write <:: COMMIT & RV32CONTROL.writeRegister;
    control RV32CONTROL <@clock_CPUdecoder> (
        cacheselect <: cacheselect,
        DMAACTIVE <: DMAACTIVE[0,1],
        SMT <:: SMT,
        COMMIT_SMT <:: COMMIT_SMT,
        frd <: frd,
        write <: write,
        result <: result,
        instruction <: instruction,
        accesssize :> accesssize
    );

    // RISC-V ADDRESS GENERATOR
    addressgenerator AGU <@clock_CPUdecoder> (
        instruction <: instruction,
        immediateValue <: RV32CONTROL.immediateValue,
        PC <: PC,
        sourceReg1 <: RV32CONTROL.sourceReg1,
        AMO <: RV32CONTROL.IS_ATOMIC,
        compressed <: compressed,
        incPC <: RV32CONTROL.incPC,
        takeBranch <: takeBranch
    );

    // FETCH UNIT
    fetch FETCH(
        clock_CPUdecoder <: clock_CPUdecoder,
        PC <: PC,
        readdata <: readdata,
        memorybusy <: memorybusy
    );

    // LOAD UNIT
    load LOAD(
        clock_CPUdecoder <: clock_CPUdecoder,
        loadAddress <: AGU.loadAddress,
        accesssize <: accesssize,
        dounsigned <: RV32CONTROL.function3[2,1],
        readdata <: readdata,
        memorybusy <: memorybusy
    );

    // STORE SELECTION + BYPASS FLAG IF 16/8 BIT WRITE TO I/O MEMORY
    uint32  storeValue <:: RV32CONTROL.FASTPATH ? EXECUTEFAST.memoryoutput : EXECUTESLOW.memoryoutput;
    uint1   STORE168FAST <:: ( ~AGU.storeAddress[26,1] & AGU.storeAddress[15,1] & ~accesssize[1,1] );
    store STORE(
        storeAddress <: AGU.storeAddress,
        storeValue <: storeValue,
        accesssize <: accesssize,
        memorybusy <: memorybusy
    );

    // EXECUTE MULTICYCLE INSTRUCTIONS, INTEGER DIVIDE, FPU, CSR AND ALU-A
    cpuexecuteSLOWPATH EXECUTESLOW(
        SMT <: SMT,
        instruction <: instruction,
        opCode <: RV32CONTROL.opCode,
        function3 <: RV32CONTROL.function3,
        function7 <: RV32CONTROL.function7,
        rs1 <: RV32CONTROL.rs1,
        rs2 <: RV32CONTROL.rs2,
        sourceReg1 <: RV32CONTROL.sourceReg1,
        sourceReg2 <: RV32CONTROL.sourceReg2,
        abssourceReg1 <: RV32CONTROL.sourceReg1abs,
        abssourceReg2 <: RV32CONTROL.sourceReg2abs,
        sourceReg1F <: RV32CONTROL.sourceReg1F,
        sourceReg2F <: RV32CONTROL.sourceReg2F,
        sourceReg3F <: RV32CONTROL.sourceReg3F,
        memoryinput <: LOAD.memoryinput,
        incCSRinstret <: COMMIT,
        isALUM <: RV32CONTROL.IS_ALUM,
        isALUCLM <: RV32CONTROL.IS_ALUCLM,
        isCSR <: RV32CONTROL.IS_CSR,
        isATOMIC <: RV32CONTROL.IS_ATOMIC,
        isFPU <: RV32CONTROL.IS_FPU,
        FASTPATHFPU <: RV32CONTROL.FASTPATHFPU
    );

    // EXECUTE SINGLE CYLE INSTRUCTIONS, MOST OF BASE PLUS INTEGER MULTIPLICATION
    uint1   takeBranch <:: RV32CONTROL.FASTPATH & EXECUTEFAST.takeBranch;
    cpuexecuteFASTPATH EXECUTEFAST(
        opCode <: RV32CONTROL.opCode,
        function3 <: RV32CONTROL.function3,
        function7 <: RV32CONTROL.function7,
        rs1 <: RV32CONTROL.rs1,
        rs2 <: RV32CONTROL.rs2,
        sourceReg1 <: RV32CONTROL.sourceReg1,
        sourceReg2 <: RV32CONTROL.sourceReg2,
        negSourceReg2 <: RV32CONTROL.sourceReg2neg,
        sourceReg2F <: RV32CONTROL.sourceReg2F,
        immediateValue <: RV32CONTROL.immediateValue,
        memoryinput <: LOAD.memoryinput,
        AUIPCLUI <: AGU.AUIPCLUI,
        nextPC <: AGU.nextPC,
        isALUMM <: RV32CONTROL.IS_ALUM,
        isLOAD <: RV32CONTROL.IS_LOAD,
        isBRANCH <: RV32CONTROL.IS_BRANCH,
        isAUIPCLUI <: RV32CONTROL.IS_AUIPCLUI,
        isJAL <: RV32CONTROL.IS_JAL
    );

    // MINI DMA CONTROLLER
    dma DMA( DMASOURCE <: DMASOURCE, DMADEST <: DMADEST, DMACOUNT <: DMACOUNT, DMAMODE <: DMAMODE, DMAACTIVE :> DMAACTIVE, readdata <: readdata, memorybusy <: memorybusy );

    // SMALL ON CPU L0 CACHE - 1 per HART - RESET when pc == 0 @ START OF BIOS
    // L0 INSTRUCTION CACHE { tag, instruction, compressed, valid } split into units
    $$for i=0,(L00Iblocks-1) do
        simple_dualport_bram uint31 L00cache_$i$ <@clock_cache,@clock> [$L0Isize$] = uninitialized;
        simple_dualport_bram uint$L00Ipartaddresswidth$ L00tags_$i$ <@clock_cache,@clock> [$L0Isize$] = uninitialized;
    $$end
    $$for i=0,(L01Iblocks-1) do
        simple_dualport_bram uint31 L01cache_$i$ <@clock_cache,@clock> [$L0Isize$] = uninitialized;
        simple_dualport_bram uint$L01Ipartaddresswidth$ L01tags_$i$ <@clock_cache,@clock> [$L0Isize$] = uninitialized;
    $$end

    il00cache_read ICACHE_0 <@clock_cache> (
        $$for i=0,(L00Iblocks-1) do
            L0cache_$i$ <:> L00cache_$i$, L0tags_$i$ <:> L00tags_$i$,
        $$end
        address <: pc
    );
    il01cache_read ICACHE_1 <@clock_cache> (
        $$for i=0,(L01Iblocks-1) do
            L0cache_$i$ <:> L01cache_$i$, L0tags_$i$ <:> L01tags_$i$,
        $$end
        address <: pcSMT
    );
    il00cache_write ICACHE_0W(
        $$for i=0,(L00Iblocks-1) do
            L0cache_$i$ <:> L00cache_$i$, L0tags_$i$ <:> L00tags_$i$,
        $$end
        address <: pc, newinstruction <: instruction[2,30], newcompressed <: compressed
    );
    il01cache_write ICACHE_1W(
        $$for i=0,(L01Iblocks-1) do
            L0cache_$i$ <:> L01cache_$i$, L0tags_$i$ <:> L01tags_$i$,
        $$end
        address <: pcSMT, newinstruction <: instruction[2,30], newcompressed <: compressed
    );
    uint1   L0match <:: ( ~SMT & ICACHE_0.tagmatch ) | ( SMT & ICACHE_1.tagmatch );
    uint1   L0update_0 <:: ~SMT & ~ICACHE_0.tagmatch;
    uint1   L0update_1 <:: SMT & ~ICACHE_1.tagmatch;
    ICACHE_0W.update := 0; ICACHE_1W.update := 0; ICACHE_0.invalidate := ~|pc; ICACHE_1.invalidate := ~|pc; ICACHE_0W.invalidate := ~|pc; ICACHE_1W.invalidate := ~|pc;

    // CONTROLS FOR MEMORY ACCESSES
    FETCH.start := 0; LOAD.start := 0; STORE.start := 0; DMA.start := 0; readmemory := 0; writememory := 0; EXECUTESLOW.start := 0; COMMIT := 0;

    algorithm <autorun> {
        while( memorybusy | EXECUTESLOW.busy ) {}                                                                                   // WAIT FDR MEMORY AND CPU TO FINISH
        cacheselect = 0; FETCH.start = 1;                                                                                           // FETCH FIRST INSTRUCTION

        while(1) {
            COMMIT_SMT = SMT;

            if( L0match ) {                                                                                                         // FETCH INSTRUCTION
                instruction =  { SMT ? ICACHE_1.instruction : ICACHE_0.instruction, 2b11 };                                             // FROM CACHE
                compressed = SMT ? ICACHE_1.compressed : ICACHE_0.compressed;
            } else {
                if( FETCH.busy ) { while( FETCH.busy ) {} } else { cacheselect = 0; }                                                   // FROM MEMORY
                instruction = FETCH.instruction; compressed = FETCH.compressed;
            }
            // DECODE, REGISTER FETCH, ADDRESS GENERATION AUTOMATICALLY TAKES PLACE AS SOON AS THE INSTRUCTION IS LOADED
            ICACHE_0W.update = L0update_0; ICACHE_1W.update = L0update_1;                                                               // UPDATE ICACHE

            cacheselect = 1;                                                                                                            // SWITCH TO DATA CACHE

            if( RV32CONTROL.IS_LOAD ) {                                                                                                 // LOAD FROM MEMORY
                LOAD.start = 1; while( LOAD.busy ) {}
            } else { LOAD.start = 0; }

            if( ~RV32CONTROL.FASTPATH ) {
                ++: EXECUTESLOW.start = 1; while( EXECUTESLOW.busy ) {}                                                                 // FPU, ALU-A, INTEGER DIVISION, CLMUL, CSR
            } else {
                EXECUTESLOW.start = 0;                                                                                                  // INTEGER ALU, LOAD, STORE, BRANCH, JUMP, ETC
            }

            if( RV32CONTROL.IS_STORE ) {
                if( STORE168FAST ) {
                    address = AGU.storeAddress; writedata = storeValue[0,16]; writememory = 1;                                          // FAST STORE FOR 16 8 TO LOW MEMORY
                } else {
                    STORE.start = 1; while( STORE.busy ) {}                                                                             // STORE
                }
            } else { STORE.start = 0; }

            COMMIT = 1; pc = pc_next; pcSMT = pcSMT_next; SMT = ~SMT & SMTRUNNING;                                                      // UPDATE PC AND SWITCH THREADS IF SMT ENABLED

            if( |DMAMODE ) {                                                                                                            // PROCESS MINI-DMA ENGINE REQUESTS
                DMA.start = 1; while( |DMAACTIVE ) { }
            } else { DMA.start = 0; }

            cacheselect = 0; FETCH.start = ~L0match;                                                                                    // START FETCH OF NEXT INSTRUCTION IF NOT IN ICACHE
        } // RISC-V
    }

    always_after {
        if( |DMAACTIVE ) {
            address = DMA.address; writedata = DMA.writedata; writememory = DMA.writememory; readmemory = DMA.readmemory;               // COPY MEMORY REQUESTS FROM DMA ENGINE
        }
        if( STORE.busy ) {
            address = STORE.address; writedata = STORE.writedata; writememory = STORE.writememory;                                      // COPY MEMORY REQUESTS FROM THE STORE UNIT
        }
        if( |LOAD.busy ) {
            address = LOAD.address; readmemory = LOAD.readmemory;                                                                       // COPY MEMORY REQUESTS FROM DMA ENGINE
        }
        if( FETCH.busy ) {
            address = FETCH.address; readmemory = FETCH.readmemory;                                                                     // COPY MEMORY REQUESTS FROM DMA ENGINE
        }
    }
}

// HART ID 0 L0 CACHE
unit il00cache_read(
$$for i=0,(L00Iblocks-1) do
    simple_dualport_bram_port0 L0cache_$i$,
    simple_dualport_bram_port0 L0tags_$i$,
$$end
    input   uint$addr_width$  address,
    input   uint1   invalidate,
    output  uint30  instruction,
    output  uint1   compressed,
    output  uint1   tagmatch
) <reginputs> {
    uint$L00Ipartaddresswidth$ tag = uninitialised;

    always_before {
        $$for i=0,(L00Iblocks-1) do
            L0cache_$i$.addr0 = address[$1+L00Icount$,$L0Icacheaddrwidth$]; L0tags_$i$.addr0 = address[$1+L00Icount$,$L0Icacheaddrwidth$];
        $$end

        switch( address[1,$L00Icount$] ) {
            $$for i=0,(L00Iblocks-1) do
                case $i$: { tag = L0tags_$i$.rdata0; instruction = L00cacheI ( L0cache_$i$.rdata0 ).instruction; compressed =  L00cacheI( L0cache_$i$.rdata0 ).compressed; }
            $$end
        }
    }
    always_after {
        tagmatch = ~invalidate & |instruction & ( tag == address[$L00Ipartaddressstart$,$L00Ipartaddresswidth$] );
    }
}

unit il00cache_write(
$$for i=0,(L00Iblocks-1) do
    simple_dualport_bram_port1 L0cache_$i$,
    simple_dualport_bram_port1 L0tags_$i$,
$$end
    input   uint$addr_width$  address,
    input   uint30  newinstruction,
    input   uint1   newcompressed,
    input   uint1   update,
    input   uint1   invalidate
) <reginputs> {
    uint$L0Icacheaddrwidth+1$ count = $L0Isize$; uint$L0Icacheaddrwidth+1$ countP1 <:: count + 1; uint1 CLS <:: ( count != $L0Isize$ );

    $$for i=0,(L00Iblocks-1) do
        L0cache_$i$.wenable1 := 0; L0tags_$i$.wenable1 := 0;
    $$end

    algorithm <autorun> {
        while(1) {
            if( invalidate ) {
                count = 0;
                while( CLS ) {
                    $$for i=0,(L00Iblocks-1) do
                        L0cache_$i$.addr1 = count; L0cache_$i$.wdata1 = 0; L0cache_$i$.wenable1 = 1;
                    $$end
                    count = countP1;
                }
            }
        }
    }

    always_after {
        if( ~CLS ) {
            switch( address[1,$L00Icount$] ) {
                $$for i=0,(L00Iblocks-1) do
                    case $i$: {
                        L0cache_$i$.addr1 = address[$1+L00Icount$,$L0Icacheaddrwidth$]; L0tags_$i$.addr1 = address[$1+L00Icount$,$L0Icacheaddrwidth$];
                        L0cache_$i$.wdata1 = { newinstruction, newcompressed }; L0tags_$i$.wdata1 = address[$L00Ipartaddressstart$,$L00Ipartaddresswidth$];
                        L0cache_$i$.wenable1 =  update; L0tags_$i$.wenable1 = update;
                    }
                $$end
            }
        }
    }
}

// HART ID 1 L0 CACHE
unit il01cache_read(
$$for i=0,(L01Iblocks-1) do
    simple_dualport_bram_port0 L0cache_$i$,
    simple_dualport_bram_port0 L0tags_$i$,
$$end
    input   uint$addr_width$  address,
    input   uint1   invalidate,
    output  uint30  instruction,
    output  uint1   compressed,
    output  uint1   tagmatch
) <reginputs> {
    uint$L01Ipartaddresswidth$ tag = uninitialised;

    always_before {
        $$for i=0,(L01Iblocks-1) do
            L0cache_$i$.addr0 = address[$1+L01Icount$,$L0Icacheaddrwidth$]; L0tags_$i$.addr0 = address[$1+L01Icount$,$L0Icacheaddrwidth$];
        $$end

        switch( address[1,$L01Icount$] ) {
            $$for i=0,(L01Iblocks-1) do
                case $i$: { tag = L0tags_$i$.rdata0; instruction = L01cacheI ( L0cache_$i$.rdata0 ).instruction; compressed =  L01cacheI( L0cache_$i$.rdata0 ).compressed; }
            $$end
        }
    }
    always_after {
        tagmatch = ~invalidate & |instruction & ( tag == address[$L01Ipartaddressstart$,$L01Ipartaddresswidth$] );
    }
}

unit il01cache_write(
$$for i=0,(L01Iblocks-1) do
    simple_dualport_bram_port1 L0cache_$i$,
    simple_dualport_bram_port1 L0tags_$i$,
$$end
    input   uint$addr_width$  address,
    input   uint30  newinstruction,
    input   uint1   newcompressed,
    input   uint1   update,
    input   uint1   invalidate
) <reginputs> {
    uint$L0Icacheaddrwidth+1$ count = $L0Isize$; uint$L0Icacheaddrwidth+1$ countP1 <:: count + 1; uint1 CLS <:: ( count != $L0Isize$ );

    $$for i=0,(L01Iblocks-1) do
        L0cache_$i$.wenable1 := 0; L0tags_$i$.wenable1 := 0;
    $$end

    algorithm <autorun> {
        while(1) {
            if( invalidate ) {
                count = 0;
                while( CLS ) {
                    $$for i=0,(L01Iblocks-1) do
                        L0cache_$i$.addr1 = count; L0cache_$i$.wdata1 = 0; L0cache_$i$.wenable1 = 1;
                    $$end
                    count = countP1;
                }
            }
        }
    }

    always_after {
        if( ~CLS ) {
            switch( address[1,$L01Icount$] ) {
                $$for i=0,(L01Iblocks-1) do
                    case $i$: {
                        L0cache_$i$.addr1 = address[$1+L01Icount$,$L0Icacheaddrwidth$]; L0tags_$i$.addr1 = address[$1+L01Icount$,$L0Icacheaddrwidth$];
                        L0cache_$i$.wdata1 = { newinstruction, newcompressed }; L0tags_$i$.wdata1 = address[$L01Ipartaddressstart$,$L01Ipartaddresswidth$];
                        L0cache_$i$.wenable1 =  update; L0tags_$i$.wenable1 = update;
                    }
                $$end
            }
        }
    }
}

unit cpuexecuteSLOWPATH(
    input   uint1   start,
    output  uint1   busy(0),
    input   uint1   SMT,
    input   uint32  instruction,
    input   uint5   opCode,
    input   uint3   function3,
    input   uint7   function7,
    input   uint5   rs1,
    input   uint5   rs2,
    input   int32   sourceReg1,
    input   int32   sourceReg2,
    input   int32   abssourceReg1,
    input   int32   abssourceReg2,
    input   uint32  sourceReg1F,
    input   uint32  sourceReg2F,
    input   uint32  sourceReg3F,
    input   int32   memoryinput,
    output  uint1   frd,
    output  int32   memoryoutput,
    output  int32   result,
    input   uint1   incCSRinstret,
    input   uint1   isCSR,
    input   uint1   isATOMIC,
    input   uint1   isFPU,
    input   uint1   isALUM,
    input   uint1   isALUCLM,
    input   uint1   FASTPATHFPU
) <reginputs> {
    // M EXTENSION - DIVISION
    aluMD ALUMD( function3 <: function3[0,2], sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2, abssourceReg1 <: abssourceReg1, abssourceReg2 <: abssourceReg2 );

    // ATOMIC MEMORY OPERATIONS
    aluA ALUA( function7 <: function7, memoryinput <: memoryinput, sourceReg2 <: sourceReg2 );

    // B EXTENSION - CLMUL
    aluCLMUL ALUBCLMUL( function3 <: function3[0,2], sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2 );

    // FLOATING POINT REGISTERS CLASSIFICATION
    classifyF class1F( a <: sourceReg1F ); classifyF class2F( a <: sourceReg2F ); classifyF class3F( a <: sourceReg3F );

    // FLOATING POINT SLOW OPERATIONS - CALCULATIONS AND CONVERSIONS
    floatconvert FPUCONVERT(
        FPUflags <: CSR.FPUflags,
        function7 <: function7[2,5], rs2 <: rs2[0,1],
        sourceReg1 <: sourceReg1, abssourceReg1 <: abssourceReg1, sourceReg1F <: sourceReg1F,
        classA <: class1F.class
    );

    floatcalc FPUCALC(
        FPUflags <: CSR.FPUflags,
        opCode <: opCode, function7 <: function7[2,5],
        sourceReg1F <: sourceReg1F, sourceReg2F <: sourceReg2F, sourceReg3F <: sourceReg3F,
        classA <: class1F.class, classB <: class2F.class, classC <: class3F.class
    );

    // FLOATING POINT FAST OPERATIONS
    fpufast FPUFAST(
        FPUflags <: CSR.FPUflags,
        function3 <: function3[0,2], function7 <: function7[2,5],
        sourceReg1 <: sourceReg1, sourceReg1F <: sourceReg1F, sourceReg2F <: sourceReg2F,
        classA <: class1F.class, classB <: class2F.class
    );

    // MANDATORY RISC-V CSR REGISTERS + HARTID == 0 MAIN THREAD == 1 SMT THREAD
    uint5   FPUnewflags <:: FASTPATHFPU ? FPUFAST.FPUnewflags : fpuconvert ? FPUCONVERT.FPUnewflags : FPUCALC.FPUnewflags;
    CSRblock CSR(
        SMT <: SMT,
        instruction <: instruction,
        function3 <: function3,
        rs1 <: rs1,
        sourceReg1 <: sourceReg1,
        FPUnewflags <: FPUnewflags,
        incCSRinstret <: incCSRinstret
    );

    // Classify the instruction
    uint1   fpuconvert <:: ( opCode == 5b10100 ) & ( function7[4,3] == 3b110 );
    uint1   fpufast <:: ( isFPU & FASTPATHFPU ) | fpuconvert;
    uint1   fpucalc <:: isFPU & ~fpufast;
    uint4   operation <:: { ~|{fpufast,isATOMIC,isCSR}, fpufast, isATOMIC, isCSR };

    // START AND BUSY FLAGS
    uint1   unitbusy <:: ( FPUCALC.busy | ALUMD.busy | ALUBCLMUL.busy );
    ALUMD.start := start & isALUM;                                                                          // INTEGER DIVISION
    ALUBCLMUL.start := start & isALUCLM;                                                                    // CARRYLESS MULTIPLY
    FPUCALC.start := start & fpucalc;                                                                       // FPU CALCULATIONS
    CSR.start := start & isCSR & |function3;                                                                // CSR

    // Deal with updating fpuflags and writing to fpu registers
    CSR.updateFPUflags := 0; frd := fpuconvert ? FPUCONVERT.frd : fpufast ? FPUFAST.frd : fpucalc ? 1 : 0;

    algorithm <autorun> {
        // PROVIDE WAIT STATE FOR APPROPRIATE OPERATION
        while(1) {
            if( start ) {
                busy = 1;
                onehot( operation ) {
                    case 0: { ++: }                                                                                     // CSR
                    case 1: {}                                                                                          // ATOMIC OPERATIONS
                    case 2: { ++: }                                                                                     // FPU FAST COMPARE, MIN/MAX, CLASS, MOVE, CONVERT
                    case 3: { while( unitbusy ) {} }                                                                    // FPU CALCULATIONS AND INTEGER DIVISION
                }
                busy = 0;
                CSR.updateFPUflags = fpuconvert | fpucalc;
            }
        }
    }

    // COLLECT THE APPROPRIATE RESULT
    always_after {
        if( busy ) {
            onehot( operation ) {
                case 0: { result = |function3 ? CSR.result : 0; }                                                   // CSR
                case 1: {
                    if( function7[3,1] ) {
                        result = memoryinput; memoryoutput = ALUA.result;                                           // ATOMIC LOAD - MODIFY - STORE
                    } else {
                        result = function7[2,1] ? 0 : memoryinput;                                                  // LR.W SC.W
                        memoryoutput = sourceReg2;
                    }
                }
                case 2: { result = fpuconvert ? FPUCONVERT.result : FPUFAST.result; }                               // FPU FAST COMPARE, MIN/MAX, CLASS, MOVE, CONVERT
                case 3: { result = fpucalc ? FPUCALC.result : function3[2,1] ? ALUMD.result : ALUBCLMUL.result; }   // FPU CALCULATIONS AND INTEGER DIVISION
            }
        }
    }
}

unit cpuexecuteFASTPATH(
    input   uint5   opCode,
    input   uint3   function3,
    input   uint7   function7,
    input   uint5   rs1,
    input   uint5   rs2,
    input   int32   sourceReg1,
    input   int32   sourceReg2,
    input   int32   negSourceReg2,
    input   uint32  sourceReg2F,
    input   int32   immediateValue,
    input   int32   memoryinput,
    input   uint32  AUIPCLUI,
    input   uint32  nextPC,
    input   uint1   isALUMM,
    input   uint1   isLOAD,
    input   uint1   isBRANCH,
    input   uint1   isAUIPCLUI,
    input   uint1   isJAL,
    output  uint1   takeBranch,
    output  int32   memoryoutput,
    output  int32   result
) <reginputs> {
    // COMPARISON UNIT
    compare COMPARE( sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2, immediateValue <: immediateValue, regimm <: opCode[3,1] );

    // BRANCH COMPARISON UNIT
    branchcomparison BRANCHUNIT( function3 <: function3, sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2, LT <: COMPARE.LT, LTU <: COMPARE.LTU, EQ <: COMPARE.EQ );

    // ALU
    alu ALU(
        opCode <: opCode, function3 <: function3, function7 <: function7,
        rs1 <: rs1, rs2 <: rs2,
        sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2, negSourceReg2 <: negSourceReg2,
        immediateValue <: immediateValue,
        LT <: COMPARE.LT, LTU <: COMPARE.LTU
    );

    // M EXTENSION - MULTIPLICATION
    aluMM ALUMM( function3 <: function3[0,2], sourceReg1 <: sourceReg1, sourceReg2 <: sourceReg2 );

    always_after {
        takeBranch = isBRANCH & BRANCHUNIT.takeBranch;                  // BRANCH
        memoryoutput = opCode[0,1] ? sourceReg2F : sourceReg2;          // FLOAT STORE OR STORE
        result = isAUIPCLUI ? AUIPCLUI :                                // LUI AUIPC
                            isJAL ? nextPC :                            // JAL[R]
                            isLOAD ? memoryinput :                      // [FLOAT]LOAD
                            isALUMM ? ALUMM.result : ALU.result;        // INTEGER ALU AND MULTIPLICATION
    }
}

// FETCH UNIT, INCLUDING COMPRESSED INSTRUCTION EXPANSION
unit fetch(
    input   uint1   clock_CPUdecoder,
    input   uint1   start,
    output!  uint1   busy(0),
    input!  uint27  PC,
    output  uint32  instruction,
    output  uint1   compressed,

    output! uint27  address,
    input!  uint16  readdata,
    output! uint1   readmemory,
    input!  uint1   memorybusy
) <reginputs> {
    uint27  PC2 <:: PC + 2;

    // COMPRESSED INSTRUCTION EXPANDER
    compressed00 COMPRESSED00 <@clock_CPUdecoder> ( i16 <: readdata ); compressed01 COMPRESSED01 <@clock_CPUdecoder> ( i16 <: readdata ); compressed10 COMPRESSED10 <@clock_CPUdecoder> ( i16 <: readdata );

    readmemory := 0;

    algorithm <autorun> {
        while(1) {
            if( start ) {
                busy = 1;
                address = PC; readmemory = 1; while( memorybusy ) {}                                                                                                                      // FETCH POTENTIAL COMPRESSED OR 1ST 16 BITS
                compressed = ( ~&readdata[0,2] );
                if( compressed ) {
                    switch( readdata[0,2] ) {                                                                                                               // EXPAND COMPRESSED INSTRUCTION
                        case 2b00: { instruction = { COMPRESSED00.i32, 2b11 }; }
                        case 2b01: { instruction = { COMPRESSED01.i32, 2b11 }; }
                        case 2b10: { instruction = { COMPRESSED10.i32, 2b11 }; }
                        default: {}
                    }
                } else {
                    instruction[0,16] = readdata; address = PC2; readmemory = 1; while( memorybusy ) {} instruction[16,16] = readdata;         // 32 BIT INSTRUCTION FETCH 2ND 16 BITS
                }
                busy = 0;
            } else { busy = 0; }
        }
    }
}

// LOAD UNIT - INCLUDING SIGN EXTENSION FOR 8/16 BIT SIGNED LOADS
unit load(
    input   uint1   clock_CPUdecoder,
    input   uint1   start,
    output! uint1   busy(0),
    input   uint27  loadAddress,
    input   uint2   accesssize,
    input   uint1   dounsigned,
    output  int32   memoryinput,

    output! uint27  address,
    input!  uint16  readdata,
    output! uint1   readmemory,
    input!  uint1   memorybusy
) <reginputs> {
    uint27  LA2 <:: loadAddress + 2;

    // SIGN EXTENDER FOR 8 AND 16 BIT LOADS
    signextend SIGNEXTEND <@clock_CPUdecoder> (
        readdata <: readdata,
        is16or8 <: accesssize[0,1],
        byteaccess <: loadAddress[0,1],
        dounsigned <: dounsigned
    );

    readmemory := 0;

    algorithm <autorun> {
        while(1) {
            if( start ) {
                busy = 1;
                address = loadAddress; readmemory = 1; while( memorybusy ) {}                                                                       // READ 1ST 8 or 16 BITS
                if( accesssize[1,1] ) {
                    memoryinput[0,16] = readdata; address = LA2; readmemory = 1; while( memorybusy ) {} memoryinput[16,16] = readdata;     // READ 2ND 16 BITS
                } else {
                    memoryinput = SIGNEXTEND.memory168;                                                                                                 // 8 or 16 BIT SIGN EXTENDED
                }
                busy = 0;
            }
        }
    }
}

// STORE UNIT FOR NON-FAST STORES ( to BRAM/SDRAM or 32 bit )
unit store(
    input   uint1   start,
    output! uint1   busy(0),
    input   uint27  storeAddress,
    input   int32   storeValue,
    input   uint2   accesssize,

    output! uint27  address,
    output! uint16  writedata,
    output! uint1   writememory,
    input!  uint1   memorybusy
) <reginputs> {
    uint27  SA2 <:: storeAddress + 2;
    writememory := 0;

    algorithm <autorun> {
        while(1) {
            if( start ) {
                busy = 1;
                address = storeAddress; writedata = storeValue[0,16];                                                       // STORE 8 OR 16 BIT
                writememory = 1; while( memorybusy ) {}
                if( accesssize[1,1] ) {
                    address = SA2; writedata = storeValue[16,16]; writememory = 1;  while( memorybusy ) {}                  // 32 BIT WRITE 2ND 16 BITS
                } else { writememory = 0; }
                busy = 0;
            }
        }
    }
}

// MINI DMA CONTROLLER
unit dma(
    input   uint27  DMASOURCE,
    input   uint27  DMADEST,
    input   uint27  DMACOUNT,
    input   uint3   DMAMODE,
    output! uint2   DMAACTIVE,

    input   uint1   start,
    output! uint27  address,
    output! uint16  writedata,
    output! uint1   writememory,
    output! uint1   readmemory,
    input!  uint16  readdata,
    input!  uint1   memorybusy
) <reginputs> {
    uint3   dmamode = uninitialized;
    uint27  dmasrc = uninitialised;                 uint27  dmasrc1 <:: dmasrc + 1;                     uint27  dmasrc2 <:: dmasrc + 2;
    uint27  dmadest = uninitialised;                uint27  dmadest1 <:: dmadest + 1;                   uint27  dmadest2 <:: dmadest + 2;
    uint1   dmablue <:: ( dmadest[0,4] == 4h6 );    uint27  dmacount = uninitialised;                   uint27  dmacount1 <:: dmacount - 1;
    uint16  memoryinput = uninitialised;

    writememory := 0; readmemory := 0;

    algorithm <autorun> {
        while(1) {
            if( start ) {
                dmamode = DMAMODE; dmasrc = DMASOURCE; dmadest = DMADEST; dmacount = DMACOUNT; DMAACTIVE = &DMAMODE ? 2 : 1;
                while( |dmacount ) {
                    address = dmasrc; readmemory = 1; while( memorybusy ) {}
                    memoryinput = readdata; address = dmadest;
                    if( DMAACTIVE[1,1] ) {
                        writedata = memoryinput[0,8]; writememory = 1;                                                                  // STORE TWO PIXELS TO PIXELBLOCK IN SPECIAL MODE
                        $$if gpu_50_mhz then
                        ++:                                                                                                             // 1 CYCLE FOR FASTER GPU, 2 FOR SLOWER
                        $$else
                        ++: ++:
                        $$end
                        writedata = memoryinput[8,8]; writememory = 1;
                    } else {
                        writedata = memoryinput[ { dmasrc[0,1], 3b000 }, 8 ];                                                           // DMA STORE 8 BIT
                        if( dmadest[26,1] ) { writememory = 1; while( memorybusy ) {} } else { writememory = 1; }                       // CHECK IF FAST STORE IS POSSIBLE
                    }
                    switch( dmamode ) {
                        default: {}                                                                                                     // INACTIVE / UNDEFINED
                        case 1: { dmasrc = dmasrc1; }                                                                                   // DMA multi-src to single-dest PIXEL BLOCK 7/8 bit + SDCARD WRITE
                        case 2: { dmasrc = dmasrc1; if( dmablue ) { dmadest = 27hd672; } else { dmadest = dmadest2; } }                 // DMA SPECIAL PIXEL BLOCK RGB
                        case 3: { dmasrc = dmasrc1; dmadest = dmadest1; }                                                               // DMA multi-src to multi-dest MEMCPY
                        case 4: { dmadest = dmadest1; }                                                                                 // DMA single-src to multi-dest MEMSET + SDCARD WRITE
                        case 5: {}                                                                                                      // DMA single-src to single-dest SET TILE/CBLITTER to single value
                        case 7: { dmasrc = dmasrc2; }                                                                                   // DMA 16bit to 2 pixels for PIXEL BLOCK special mode
                    }
                    dmacount = dmacount1;
                }
                DMAACTIVE = 0;
            } else { DMAACTIVE = 0; }
        }
    }
}
